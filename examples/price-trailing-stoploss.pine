// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© ZenAndTheArtOfTrading / PineScriptMastery.com
// @version=6

// PRICE TRAILING STOP

strategy("PineScriptMastery.com | Price Trailing Stop", overlay=true)

//! User inputs 
float INPUT_SL_DISTANCE     = input.float(1, "SL Distance", display=display.none)
int INPUT_MA_LENGTH         = input.int(50, "MA Length", display=display.none)

//! Indicator values
float atrValue              = ta.atr(14)
float movingAverage         = ta.sma(close, INPUT_MA_LENGTH)

//! Variables
var float tradeStopLoss     = na
float slDistance            = atrValue * INPUT_SL_DISTANCE

//! Conditions
bool canTakeTrade           = strategy.position_size == 0 and not na(atrValue) and not na(movingAverage)
bool longEntryCondition     = ta.crossover(close, movingAverage) and barstate.isconfirmed
bool shortEntryCondition    = ta.crossunder(close, movingAverage) and barstate.isconfirmed

//! Check entry conditions
if (canTakeTrade)
    //! Reset SL/TP before placing a new trade
    tradeStopLoss := na

    //! LONG ENTRY
    if (longEntryCondition)
        //! Enter
        strategy.entry("Long", strategy.long)
        //! Calculate STOP
        tradeStopLoss := close - slDistance

    //! SHORT ENTRY
    if (shortEntryCondition)
        //! Enter
        strategy.entry("Short", strategy.short)
        //! Calculate stop
        tradeStopLoss := close + slDistance

//! Long exit
if (strategy.position_size > 0)
    //! UPDATE LONG TRAILING STOP ON BAR CLOSES
    if (barstate.isconfirmed)
        float newLongSL = low - slDistance
        if (newLongSL > tradeStopLoss)
            tradeStopLoss := newLongSL

    //! LONG STOP LOSS
    strategy.exit("Exit Long", from_entry="Long", stop=tradeStopLoss)

//! Short exit
if (strategy.position_size < 0)
    //! UPDATE SHORT TRAILING STOP ON BAR CLOSES
    if (barstate.isconfirmed)
        float newShortSL = high + slDistance
        if (newShortSL < tradeStopLoss)
            tradeStopLoss := newShortSL
            
    //! SHORT STOP LOSS
    strategy.exit("Exit Short", from_entry="Short", stop=tradeStopLoss)

//! Drawing/debug
plot(tradeStopLoss, "SL", color=color.red, style=plot.style_linebr)
plot(movingAverage, "MA", color=color.blue)